# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

# Package Options - Can be overwritten by the environment

# CD_DIR: path where your Ascendancy CDROM is mounted
: ${CD_DIR:="${HOME}/media/Ascend.iso/"}

_appname="ascendancy"
pkgname=ascendancy-us
pkgdesc="Science fiction turn-based strategy game (for US versions, uses DOSBox, CD-ROM required)"
pkgver=1.6.10
pkgrel=1
arch=(any)
license=("custom")
url="http://www.logicfactory.com/main/Logic_Factory.html"
depends=("modfs-git" "dosbox" "dos32a")
makedepends=("convmv" "findutils")
provides=("${_appname}")
conflicts=("${_appname}")
source=("${_appname}.desktop"
        "${_appname}-config.desktop"
        "${_appname}.png::http://www.logicfactory.com/main/Logic_Factory_files/Ascendancy App Icon 1024x1024 cutout.png"
        "ftp://ftp.physics.auth.gr/pub/mirrors/ftp.patches-scrolls.de/pub/Ascendancy/antag.zip"
        "lib${_appname}.sh"
        "${_appname}.sh"
        "${_appname}-config.sh"
        "01-user.conf"
        "99-base.conf"
        "cob.cfg")
md5sums=('8220b2cbc77550f8ceb9fed0e9150645'
         '7f3d6de0b3faeb8cb3ae010019fee4dd'
         '66f837b34eae60d7460a27faf07f15c8'
         '842a7759c956e403aac375e83e49dfa9'
         '7985f58032a4690bd90887bb9ca3e363'
         '5678055af34ffcb70496d51457086001'
         'ebe264aa732e4f4e6e6dd231d2dc6682'
         'c1549ac03b636273288afaf4046f3ad0'
         '3ed640b29163cfff258e17f742f4e9bd'
         '64095329fffbe095fe803cc1b421b885')
install="${pkgname}.install"

package() {
  # Copy original resources (case-insensitive)
  cd "${CD_DIR}/data"
  local i
  for i in "*.cob" "*.dig" "aildrvr.lst" "setsound.exe" "uvconfig.exe"; do
    find . -maxdepth 1 -iname "${i}" -exec install -Dm644 '{}' "${pkgdir}/usr/share/games/${_appname}/base/{}" \;
  done
  
  install -Dm644 "${srcdir}/cob.cfg" "${pkgdir}/usr/share/games/${_appname}/base/cob.cfg"

  # Install 1.8.10 patch (aka the Antagonizer)
  install -Dm644 "${srcdir}/ANTAG.EXE" "${pkgdir}/usr/share/games/${_appname}/base/ascend.exe"

  cd "${pkgdir}/usr/share/games/${_appname}/base"
  
  # Use dos32a instead of dos4gw
  ln -s "/usr/share/dos32a/dos32a.exe" "dos4gw.exe"

  # Fix case
  convmv --lower --notest -r *
  
  install -Dm644 "${srcdir}/01-user.conf" "${pkgdir}/etc/${_appname}.d/01-user.conf"
  install -Dm644 "${srcdir}/99-base.conf" "${pkgdir}/etc/${_appname}.d/99-base.conf"
  install -Dm644 "${srcdir}/${_appname}.desktop" "${pkgdir}/usr/share/applications/${_appname}.desktop"
  install -Dm644 "${srcdir}/${_appname}-config.desktop" "${pkgdir}/usr/share/applications/${_appname}-config.desktop"
  install -Dm644 "${srcdir}/${_appname}.png" "${pkgdir}/usr/share/pixmaps/${_appname}.png"
  install -Dm644 "${srcdir}/lib${_appname}.sh" "${pkgdir}/usr/lib/lib${_appname}.sh"
  install -Dm755 "${srcdir}/${_appname}.sh" "${pkgdir}/usr/bin/${_appname}"
  install -Dm755 "${srcdir}/${_appname}-config.sh" "${pkgdir}/usr/bin/${_appname}-config"
}
