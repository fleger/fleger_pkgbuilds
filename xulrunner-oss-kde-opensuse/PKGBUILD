# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>
# Contributor: Weng Xuetian <wengxt@gmail.com>

pkgname=xulrunner-oss-kde-opensuse
pkgver=1.9.2.15
_ffoxver=3.6.15
pkgrel=1
pkgdesc="Mozilla Runtime Environment compiled with OSS support and with OpenSUSE patch, integrates better with KDE"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'gcc-libs' 'libidl2' 'mozilla-common' 'nss' 'libxt' 'hunspell' 'startup-notification' 'mime-types' 'dbus-glib' 'libevent' 'sqlite3>=3.7.4' 'kmozillahelper>=0.6' 'libproxy')
makedepends=('zip' 'pkg-config' 'diffutils' 'libgnomeui' 'python2' 'wireless_tools' 'autoconf2.13')
optdepends=('libgnomeui: GNOME integration and MIME handling'
            'wireless_tools: Location aware browsing')
provides=("xulrunner=${pkgver}" "xulrunner-oss=${pkgver}" "xulrunner-kde-opensuse=${pkgver}")
conflicts=('xulrunner' 'xulrunner-oss' 'xulrunner-kde-opensuse')
url="http://wiki.mozilla.org/XUL:Xul_Runner"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${_ffoxver}/source/firefox-${_ffoxver}.source.tar.bz2
        mozconfig
        mozilla-pkgconfig.patch
        fix-mozilla-launcher.patch
        xulrunner-version.patch
        xulrunner-png14.patch
        enable-x86_64-tracemonkey.patch
        python2.7.patch
        mozilla-cairo-lcd.patch
        mozilla-libproxy.patch
        mozilla-nongnome-proxies.patch
        mozilla-prefer_plugin_pref.patch
        mozilla-gconf-backend.patch
        gecko-lockdown.patch
        mozilla-kde.patch
		)

md5sums=('fcf8042948d91f1f3d9c33599b79cf35'
         '5655570cc09b650af2e9fb4dc05bd4ab'
         'd839d1c4ef736e6d89ccf91b23b965a4'
         '63eee2d1da3b43c9d604f2253f242f40'
         '371303c5bdc4fa0d955d14521b93b69d'
         '3bd0566180ad2daa32743b3ce58b2095'
         'cbd938cd1fb8210cd8a2c41833489af9'
         'ab3dc9aecae7f08b9492fb3c00a5fd28'
         'c0c8857a90daabf8fc08ffb491969971'
         'ee3cc5c1b08720a2832ced0f88be2ffe'
         'ea37f26534d1bab452da7945695e2b32'
         '641b9df7c12d302d6ca8b2aec0643270'
         '746eb01d9411a49af494690615d9a0c0'
         'a0778fea18840758123fce06c64b02af'
         'f4161608d91cc0b43e256b749dbe32c7')


build() {
  cd "${srcdir}/mozilla-1.9.2"
  cp "${srcdir}/mozconfig" .mozconfig

  rm -f toolkit/xre/nsKDEUtils.cpp \
        toolkit/xre/nsKDEUtils.h \
        uriloader/exthandler/unix/nsCommonRegistry.cpp \
        uriloader/exthandler/unix/nsCommonRegistry.h \
        uriloader/exthandler/unix/nsKDERegistry.cpp \
        uriloader/exthandler/unix/nsKDERegistry.h \
        toolkit/system/unixproxy/nsLibProxySettings.cpp \
        toolkit/content/widgets/dialog-kde.xml \
        toolkit/content/widgets/preferences-kde.xml \
        extensions/pref/system-pref/src/nsISystemPrefService.h
  
  #Use OSS instead of ALSA
  sed -i 's/sydney_audio_alsa/sydney_audio_oss/' media/libsydneyaudio/src/Makefile.in || return 1
  
  #Get rid of ALSA stuff in the build system
  sed -i '/alsa\//d' config/system-headers || return 1
  sed -i '/alsa\//d' js/src/config/system-headers || return 1
  sed -i '/LIB(asound/d' configure.in || return 1
  autoconf-2.13 || return 1
  
  msg "patch mozilla-cairo-lcd"
  patch -Np1 -i "${srcdir}/mozilla-cairo-lcd.patch" || return 1

  msg "patch mozilla-libproxy"
  patch -Np1 -i "${srcdir}/mozilla-libproxy.patch" || return 1

  msg "patch mozilla-nongnome-proxies"
  patch -Np0 -i "${srcdir}/mozilla-nongnome-proxies.patch" || return 1

  msg "patch mozilla-prefer_plugin_pref"
  patch -Np1 -i "${srcdir}/mozilla-prefer_plugin_pref.patch" || return 1

  msg "patch mozilla-gconf-backend"
  patch -Np1 -i "${srcdir}/mozilla-gconf-backend.patch" || return 1

  msg "patch gecko-lockdown"
  patch -Np1 -i "${srcdir}/gecko-lockdown.patch" || return 1

  msg "patch mozilla-kde"
  patch -Np1 -i "${srcdir}/mozilla-kde.patch" || return 1

  #fix libdir/sdkdir - fedora
  patch -Np1 -i "${srcdir}/mozilla-pkgconfig.patch"

  #Fix stub launcher - archlinux
  patch -Np0 -i "${srcdir}/fix-mozilla-launcher.patch"

  #Force installation to the same path for every version
  patch -Np1 -i "${srcdir}/xulrunner-version.patch"

  #Fix compile with libpng 1.4
  patch -Np0 -i "${srcdir}/xulrunner-png14.patch"

  #Tracemonkey for x86_64
  patch -Np0 -i "${srcdir}/enable-x86_64-tracemonkey.patch"

  #python2.7
  patch -Np0 -i "${srcdir}/python2.7.patch"

  unset CFLAGS
  unset CXXFLAGS

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd "${srcdir}/mozilla-1.9.2"
  make -j1 DESTDIR="${pkgdir}" install

  #Remove included dictionaries, add symlink to system myspell path.
  #Note: this will cause file conflicts when users have installed dictionaries in the old location
  rm -rf "${pkgdir}/usr/lib/xulrunner-1.9.2/dictionaries"
  ln -sf /usr/share/myspell/dicts "${pkgdir}/usr/lib/xulrunner-1.9.2/dictionaries"

  # add xulrunner library path to ld.so.conf
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo "/usr/lib/xulrunner-1.9.2" > ${pkgdir}/etc/ld.so.conf.d/xulrunner.conf
}
