# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>
pkgname=foobar2000
pkgver=1.1.5
pkgrel=1
pkgdesc="An advanced freeware audio player (uses Wine)."
arch=(any)
url="http://www.foobar2000.org/"
license=('custom')
depends=(wine desktop-file-utils)
makedepends=(p7zip)
source=("http://www.freecodecs.net/fc/foobar2000_v${pkgver}.zip"
        "LICENSE"
        ${pkgname}.sh
        "http://upload.wikimedia.org/wikipedia/en/7/7c/Foobar2000_Icon.svg"
        ${pkgname}.desktop)
install=${pkgname}.install
md5sums=('65d031a69726ce037f37a4d29b636e45'
         '91873c4c3115bdd1afcf52c3023dc896'
         '63f65a22001873c7b765e06f6c66707f'
         'f417c64b07b55da01f06345861511386'
         '138cb528cb0cad4e2764c3c77555369c')

package() {
    # Set up the environment
    local tmpdir="${srcdir}/foobar2000"
    local -A files

    files=( ["/"]="foobar2000*.exe *.html *.css zlib1.dll shared.dll"
        ["components"]="foo_*.dll"
        ["icons"]="*.ico"
        ["themes"]="*.fth"
    )
    
    local f
    local d

    mkdir -p "${tmpdir}" || return 1

    # Extract foobar (7z x gives garbage)
    7z e -y -o"${tmpdir}" "${srcdir}/${pkgname}_v${pkgver}.exe" || return 1

    # Installation
    cd "${tmpdir}"
    for d in "${!files[@]}"; do
          for f in ${files[$d]}; do
                install -D -m644 "${tmpdir}/${f}" "${pkgdir}/usr/share/${pkgname}/${d}/${f}" || return 1
          done
    done

    # Install the license
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1

    # Install the script
    install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}" || return 1

    # Install the icon
    install -D -m644 "${srcdir}/Foobar2000_Icon.svg" "${pkgdir}/usr/share/icons/${pkgname}.svg" || return 1

    # Install the .desktop
    install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop" || return 1
}
