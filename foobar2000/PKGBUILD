# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>
pkgname=foobar2000
pkgver=1.1.12a
pkgrel=1
pkgdesc="An advanced freeware audio player (uses Wine)."
arch=(i686 x86_64)
url="http://www.foobar2000.org/"
license=('custom')
depends=(wine desktop-file-utils)
makedepends=(p7zip)
source=("http://www.freecodecs.net/fc/foobar2000_${pkgver}.zip"
        "LICENSE"
        "${pkgname}.sh"
        "http://upload.wikimedia.org/wikipedia/en/f/f9/Foobar2000_Icon.png"
        "${pkgname}.desktop")
install="${pkgname}.install"
md5sums=('3a2745da1ae0a608d4b133864d76664c'
         '91873c4c3115bdd1afcf52c3023dc896'
         '6e2884a32aba29c9e028e09d4817489b'
         '927c185d2f680b44faf1a47f528905c7'
         '138cb528cb0cad4e2764c3c77555369c')

package() {
  # Set up the environment
  local tmpdir="${srcdir}/foobar2000"
  local -A files

  files=( ["/"]="foobar2000*.exe *.html *.css zlib1.dll shared.dll"
    ["components"]="foo_*.dll"
    ["icons"]="*.ico"
    ["themes"]="*.fth"
  )

  local f
  local d

  mkdir -p "${tmpdir}"

  # Extract foobar (7z x gives garbage)
  7z e -y -o"${tmpdir}" "${srcdir}/${pkgname}_v${pkgver}.exe"

  # Installation
  cd "${tmpdir}"
  for d in "${!files[@]}"; do
    for f in ${files[$d]}; do
      install -Dm644 "${tmpdir}/${f}" "${pkgdir}/usr/share/${pkgname}/${d}/${f}"
    done
  done

  # Install the license
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install the script
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Install the icon
  install -Dm644 "${srcdir}/Foobar2000_Icon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # Install the .desktop
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
