# Maintainer: Florian LÃ©ger <florian6.leger@laposte.net>
pkgname=ashd
pkgver=0.11
pkgrel=1
pkgdesc="A Sane HTTP Daemon."
arch=(i686 x86_64)
url="http://www.dolda2000.com/~fredrik/ashd/"
license=('GPL')
source=("http://www.dolda2000.com/~fredrik/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "ashd"
        "ashd.conf")
depends=("gnutls" "file" "attr")
md5sums=('ea552a91dc5a4d87741dd1bb97cf323a'
         '5611d757b7d4695e721f5cc3600d39b1'
         '2ee675c226fa5396bb65fad1b0035e50')
backup=("etc/conf.d/ashd.conf" "etc/ashd/dirplex.rc" "etc/ashd/apache-compat")
install="ashd.install"

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 etc/ashd/dirplex.rc "$pkgdir/etc/ashd/dirplex.rc"
  install -Dm644 etc/ashd/apache-compat "$pkgdir/etc/ashd/apache-compat"
  install -d "$pkgdir/etc/ashd/dirplex.d"
  install -m644 -t "$pkgdir/etc/ashd/dirplex.d" "etc/ashd/dirplex.d/"*.rc
  install -d "$pkgdir/usr/share/ashd/examples/"
  cp -r "examples/"* "$pkgdir/usr/share/ashd/examples/"

  cd "$srcdir"
  install -Dm755 ashd "$pkgdir/etc/rc.d/ashd"
  install -Dm644 ashd.conf "$pkgdir/etc/conf.d/ashd.conf"
}
