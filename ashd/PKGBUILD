# Maintainer: Florian LÃ©ger <florian6.leger@laposte.net>
pkgname=ashd
pkgver=0.7
pkgrel=1
pkgdesc="A Sane HTTP Daemon."
arch=(i686 x86_64)
url="http://www.dolda2000.com/~fredrik/ashd/"
license=('GPL')
source=(http://www.dolda2000.com/~fredrik/${pkgname}/${pkgname}-${pkgver}.tar.gz
        ashd
        ashd.conf)
depends=(gnutls file)
md5sums=('1050799ca8c423541be201eec991c7e8'
         '5611d757b7d4695e721f5cc3600d39b1'
         '2ee675c226fa5396bb65fad1b0035e50')

backup=(etc/conf.d/ashd.conf etc/dirplex.rc etc/apache-compat)
install=ashd.install

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr || return 1
  make || return 1
  
  cd examples
  find . -type f -exec sed -i -e 's|#!/usr/bin/python| #!/usr/bin/env python2|' "{}" \;
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install || return 1
  install -Dm644 etc/ashd/dirplex.rc "$pkgdir/etc/dirplex.rc"
  install -Dm644 etc/ashd/apache-compat "$pkgdir/etc/apache-compat"
  install -d "$pkgdir/etc/dirplex.d"
  install -m644 -t "$pkgdir/etc/dirplex.d" "etc/ashd/dirplex.d/"*.rc
  install -d "$pkgdir/usr/share/ashd/examples/"
  cp -r "examples/"* "$pkgdir/usr/share/ashd/examples/"
 
  cd "$srcdir"
  install -Dm755 ashd "$pkgdir/etc/rc.d/ashd"
  install -Dm644 ashd.conf "$pkgdir/etc/conf.d/ashd.conf"
}
