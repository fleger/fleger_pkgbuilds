# Maintainer: Florian LÃ©ger <florian6.leger@laposte.net>
pkgname=ashd
pkgver=0.5
pkgrel=1
pkgdesc="A Sane HTTP daemon."
arch=(i686 x86_64)
url="http://www.dolda2000.com/~fredrik/ashd/"
license=('GPL')
source=(http://www.dolda2000.com/~fredrik/${pkgname}/${pkgname}-${pkgver}.tar.gz
        ashd
        ashd.conf)
depends=(gnutls file)
md5sums=('b858663cd9bdefee1c095f5ee77b32c0'
         '6ed33e72b383690197794729803385aa'
         'f928fe34c998b7face9c4a1f4656b586')

backup=(etc/conf.d/ashd.conf)
options=('emptydirs')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install || return 1
  install -Dm644 etc/ashd/dirplex.rc "$pkgdir/etc/ashd/dirplex.rc"
  install -Dm644 etc/ashd/apache-compat "$pkgdir/etc/ashd/apache-compat"
  install -d "$pkgdir/etc/ashd/dirplex.d"
  install -m644 -t "$pkgdir/etc/ashd/dirplex.d" "etc/ashd/dirplex.d/"*.rc
 
  cd "$srcdir"
  install -Dm755 ashd "$pkgdir/etc/rc.d/ashd"
  install -Dm644 ashd.conf "$pkgdir/etc/conf.d/ashd.conf"
}
