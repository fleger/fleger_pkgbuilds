# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgname=darkforces
pkgver=1.0
pkgrel=3
pkgdesc="FPS set in the Star Wars universe (uses DOSBox)"
arch=("any")
depends=("dosbox" "dos32a" "modfs-git")
makedepends=("convmv")
license=("custom")
url="http://www.lucasarts.com"
source=("cutscene.lst"
        "lib${pkgname}.sh"
        "${pkgname}.sh"
        "${pkgname}-config.sh"
        "${pkgname}.desktop"
        "${pkgname}-config.desktop"
        "${pkgname}.png"
        "01-user.conf"
        "99-data.conf")
md5sums=('f46ca20bf1fc9d420b959c693947c6fc'
         'a02815d6fdcaa40cf92388d52f0eeb37'
         '6edc7ab1faaebea9644e9dc98c6db12c'
         '84e8deee94160f2d019c2badeed6ed67'
         'd7f8d149736174cd778e1bab7a178002'
         '8dc304f75e1c3e2bfa3d63f7cfd3aae4'
         '341b165346b0ed1769fa012b9f8b2d9e'
         '987f9accdaea0898f76814bc6927828c'
         '71e436f08b5c14ca6e1777506082d635')

# Change this to the path where your Dark Forces CDROM is mounted
: ${CD_DIR:="$HOME/media/Star_Wars__Dark_Forces_(FR).iso/"}

package() {
  # Copy data (case insensitive)
  cd "${CD_DIR}"

  local f
  for f in "./cd.id" "./dark/*.gob" "./dark/*.ini" "./dark/*.cfg" "./dark/*.msg" "./dark/lfd/*.lfd" "./dark/dark.exe" "./dark/imuse.exe"; do
    find . -maxdepth 3 -iwholename "${f}" -exec install -Dm644 "{}" "${pkgdir}/usr/share/games/${pkgname}/data/{}" \;
  done

  for f in install.exe bootmkr.exe readme.txt; do
    find . -maxdepth 1 -iname "${f}" -exec install -Dm644 "{}" "${pkgdir}/usr/share/games/${pkgname}/data/dark/{}" \;
  done
  
  # Fix case
  cd "${pkgdir}/usr/share/games/${pkgname}/data"
  convmv --lower --notest -r *

  # Restore a missing cutscene
  install -Dm644 "${CD_DIR}/lecdemos/dfdemo/lfd/gearup.lfd" "${pkgdir}/usr/share/games/${pkgname}/data/dark/lfd/gearup.lfd"
  install -Dm644 "${srcdir}/cutscene.lst" "${pkgdir}/usr/share/games/${pkgname}/data/dark/cutscene.lst"

  # Use DOS32/A instead of DOS4/GW
  ln -s "/usr/share/dos32a/dos32a.exe" "${pkgdir}/usr/share/games/${pkgname}/data/dark/dos4gw.exe"

  # Tell Dark Forces to look at drive C
  echo -n "C" > "${pkgdir}/usr/share/games/${pkgname}/data/dark/drive.cd"

  # Shell scripts
  install -Dm644 "${srcdir}/lib${pkgname}.sh" "${pkgdir}/usr/lib/lib${pkgname}.sh"
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 "${srcdir}/${pkgname}-config.sh" "${pkgdir}/usr/bin/${pkgname}-config"

  # Install .desktop files
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${srcdir}/${pkgname}-config.desktop" "${pkgdir}/usr/share/applications/${pkgname}-config.desktop"

  # Install icon
  install -Dm644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  
  # Modfs
  install -Dm644 "${srcdir}/01-user.conf" "${pkgdir}/etc/${pkgname}.d/01-user.conf"
  install -Dm644 "${srcdir}/99-data.conf" "${pkgdir}/etc/${pkgname}.d/99-data.conf"
}

