# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgname=darkforces
pkgver=1.0
pkgrel=2
pkgdesc="FPS set in the Star Wars universe (uses DOSBox)"
arch=("any")
depends=("dosbox" "dos32a")
license=("custom")
url="http://www.lucasarts.com"
source=("drive.cd"
        "cutscene.lst"
        "${pkgname}.sh"
        "${pkgname}.desktop"
        "${pkgname}.png")
md5sums=('f623e75af30e62bbd73d6df5b50bb7b5'
         'f46ca20bf1fc9d420b959c693947c6fc'
         '5644b708e04a5dafa815c45d682cc8c6'
         'bd65d5bce1705b89b1bbb0b86610ec4a'
         '341b165346b0ed1769fa012b9f8b2d9e')

# Change this to the path where your Dark Forces CDROM is mounted
_cddir="$HOME/media/Star_Wars__Dark_Forces_(FR).iso/"

package() {
  install -D -d "${pkgdir}/usr/share/games/${pkgname}" || return 1
  cd "${_cddir}" || return 1

  local f
  for f in cd.id dark/*.gob dark/*.ini dark/*.cfg dark/*.msg dark/dark.exe dark/imuse.exe dark/lfd/*.lfd
  do
    install -Dm644 "${f}" "${pkgdir}/usr/share/games/${pkgname}/${f}" || return 1
  done

  for f in install.exe bootmkr.exe readme.txt
  do
    install -Dm644 "${f}" "${pkgdir}/usr/share/games/${pkgname}/dark/${f}" || return 1
  done

  # Restore a missing cutscene
  install -D -m644 "${_cddir}/lecdemos/dfdemo/lfd/gearup.lfd" "${pkgdir}/usr/share/games/${pkgname}/dark/lfd/gearup.lfd" || return 1
  install -D -m644 "${srcdir}/cutscene.lst" "${pkgdir}/usr/share/games/${pkgname}/dark/cutscene.lst" || return 1

  # Use DOS32/A instead of DOS4/GW
  ln -s "/usr/share/dos32a/dos32a.exe" "${pkgdir}/usr/share/games/${pkgname}/dark/dos4gw.exe" || return 1

  # Tell Dark Forces to look at drive d
  install -D -m644 "${srcdir}/drive.cd" "${pkgdir}/usr/share/games/${pkgname}/dark/drive.cd" || return 1

  install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}" || return 1

  # Install .desktop file
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop" || return 1

  # Install icon
  install -D -m644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/icons/${pkgname}.png" || return 1
}

