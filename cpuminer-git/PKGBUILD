# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgname=cpuminer-git
pkgver=20110215
pkgrel=1
pkgdesc="Multi-threaded CPU miner for bitcoin."
arch=('i686' 'x86_64')
url="http://yyz.us/bitcoin/"
license=('GPLv2')
depends=('curl' 'jansson')
makedepends=('git')
source=(rc-script.cpuminer
        cpuminer.conf)
backup=(etc/conf.d/cpuminer.conf etc/cpuminer.json)
md5sums=('a014ba1a7ea4240c585be90822db9315'
         '96ee832cea2ff946bb890669cc7d34a0')

_gitroot="http://github.com/jgarzik/cpuminer.git"
_gitname="cpuminer"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  ./autogen.sh || return 1
  # Here you may want to use custom CFLAGS
  #export CFLAGS="-O2 -march=native -mtune=native -msse2"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install || return 1
  install -Dm600 "example-cfg.json" "$pkgdir/etc/cpuminer.json"
  install -Dm644 "$srcdir/cpuminer.conf" "$pkgdir/etc/conf.d/cpuminer.conf" || return 1
  install -Dm755 "$srcdir/rc-script.cpuminer" "$pkgdir/etc/rc.d/cpuminer" || return 1
}

