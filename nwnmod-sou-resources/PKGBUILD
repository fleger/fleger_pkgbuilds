# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgname=nwnmod-sou-resources
pkgdesc="Shadows Of Undrentide Neverwinter Nights expansion resources."
pkgver=1.69
pkgrel=1
arch=(any)
depends=("nwnmod-original-resources=$pkgver")
license=("custom")
url="http://nwn.bioware.com/downloads/linuxclient.html"
source=("http://files.bioware.com/neverwinternights/updates/linux/${pkgver//.}/English_linuxclient${pkgver//.}_xp1.tar.gz"
        "98-sou-resources.conf")
md5sums=('8b41c88366e1de8933d48b178ca3de0e'
         '4080e1e473dcfd42d2cf14d2869139e6')
# Package options
# Path to the SOU cdrom mountpoint
_cddir="/media/cdrom"

package() {
  cd "$srcdir"
  install -Dm644 "98-sou-resources.conf" "${pkgdir}/etc/nwnmod.d/98-sou-resources.conf"
  install -d "${pkgdir}/opt/nwnmod-sou-resources"
  # Data_Shared.zip
  unzip "$_cddir/Data_Shared.zip" 'data/*' 'music/*' 'texturepacks/*' -d "${pkgdir}/opt/nwnmod-sou-resources"
  # Language_Data.zip
  unzip "$_cddir/Language_Data.zip" -x xp1patch.key data/xp1patch.bif  -d "${pkgdir}/opt/nwnmod-sou-resources"
  # Patch
  local f
  for f in xp1patch.key data/xp1patch.bif nwm/XP1*.nwm; do
    install -Dm644 "$f" "${pkgdir}/opt/nwnmod-sou-resources/$f"
  done
}