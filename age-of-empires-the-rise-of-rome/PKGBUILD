# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

# Package Options - Can be overwritten by the environment

# CD_DIR: path where your Age Of Empires: The Rise Of Rome CD-ROM is mounted.
# Note: both Standalone and Gold editions are supported.
: ${CD_DIR:="/media/sr0"}

_appname="ror"
pkgname=age-of-empires-the-rise-of-rome
pkgdesc="History-based real-time strategy computer game (uses Wine, CD-ROM required)"
pkgver=1.0a
pkgrel=1
arch=(any)
license=("custom")
url="http://www.microsoft.com/games/aoeexpansion/"
depends=("age-of-empires")
makedepends=("imagemagick" "icoutils" "cabextract")
source=("${_appname}.desktop"
        "http://download.microsoft.com/download/aoeror/Patch/1.0a/W9X/EN-US/aoepatch.exe"
        "${_appname}.sh"
        "98-${_appname}.conf")
md5sums=('e986c1e62e591a1421d7733f8d98420c'
         'd70583332d80bb930b95eceea989ffa2'
         '9f9f99823cb9fb7c07489f98dd825751'
         '5da368350b84b05a00c90784d844b330')

shopt -s nocaseglob

build() {
  # Icon
  convert "${CD_DIR}/aoex.ico" "${srcdir}/${_appname}.png"
  
  # Patch
  cd "${srcdir}"
  cabextract -L -d . "aoepatch.exe"
}

package() {
  local i

  # Copy original resources
  cd "${CD_DIR}/"

  # License
  for i in cluf.*; do
    install -Dm644 "${i}" "${pkgdir}/usr/share/licenses/${pkgname}/${i,,*}"
  done

  # Docs
  local docsDir="docs"                                                  # Standalone Edition
  [ -d "docs/RiseRome" ] && docsDir="docs/RiseRome"                     # Gold Edition
  for i in "${docsDir}/"*.doc "${docsDir}/"*.pdf; do
    install -Dm644 "${i}" "${pkgdir}/usr/share/doc/${_appname}/$(basename ${i,,*})"
  done

  # Game
  cd game
  for i in "aelaunch.dll" "avi/introx.avi" \
           "campaign/"*.cpx  "data2/"* "sound/x"*.mid; do
    install -Dm644 "${i}" "${pkgdir}/usr/share/games/aoe/${_appname}/${i,,*}"
  done

  install -Dm644 "langua_1.dll" "${pkgdir}/usr/share/games/aoe/${_appname}/languagex.dll"

  # Patch
  install -Dm644 "${srcdir}/empiresx.exe" "${pkgdir}/usr/share/games/aoe/${_appname}/empiresx.exe"

  # Modfs
  install -Dm644 "${srcdir}/98-${_appname}.conf" "${pkgdir}/etc/aoe.d/98-${_appname}.conf"
  
  # .desktop
  install -Dm644 "${srcdir}/${_appname}.desktop" "${pkgdir}/usr/share/applications/${_appname}.desktop"
  
  # Icon
  install -Dm644 "${srcdir}/${_appname}-5.png" "${pkgdir}/usr/share/pixmaps/${_appname}.png"
  
  # Shell scripts
  install -Dm755 "${srcdir}/${_appname}.sh" "${pkgdir}/usr/bin/${_appname}"
}
