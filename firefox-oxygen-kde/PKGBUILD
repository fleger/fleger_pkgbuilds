# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>
pkgname=firefox-oxygen-kde
pkgver=3.6
pkgrel=2
pkgdesc="Complete add-on/theme bringing the Oxygen style for Firefox."
arch=(any)
depends=("firefox>=8.0")
makedepends=("unzip")
url="http://oxygenkde.altervista.org/index.html"
license=('GPL')
source=("http://oxygenkde.altervista.org/download/OxygenKDE_${pkgver//./_}.xpi")
md5sums=('f6362903f012f0c7fb5ffc013fbca962')

package() {
  local f
  local emid
  local destdir

  cd "${srcdir}"

  for f in "oxykde_opt${pkgver//.}" "oxykde_theme${pkgver//.}"; do
    unzip -o -d "${f}" "${f}.xpi"
    emid=$(sed -n -e '/<\?em:id>\?/!d; s/.*\([\"{].*[}\"]\).*/\1/; s/\"//g; p; q' "${f}/install.rdf")
    sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>9.*</em:maxVersion>#' "${f}/install.rdf"
    dstdir="${pkgdir}/usr/lib/firefox/extensions/${emid}"
    install -d "${dstdir}"
    pushd "${f}"
    find . -type f -exec install -Dm644 '{}' "${dstdir}/{}" \;
    popd
  done
}
