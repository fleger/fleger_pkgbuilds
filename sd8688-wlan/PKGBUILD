# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgname=sd8688-wlan
pkgver=10.49.3.p70
pkgrel=1
pkgdesc="WLAN driver for Marvell SD8688 WiFi/BlueTooth chipset."
arch=('i686' 'x86_64' 'arm')
url='http://plugcomputer.org/plugwiki/index.php/Setting_GuruPlug_to_be_a_stable_WiFi_Client'
license=('GPL')
depends=('kernel26' 'sd8688-firmware')
makedepends=('kernel26-headers' 'gzip')
install='sd8688-wlan.install'
source=("http://plugcomputer.org/plugwiki/images/0/05/SD_WLAN.tar.gz"
        "sd_wlan.diff")
md5sums=('2f279f30bee27ca52118b931b39a922b'
         '7851fe80b0a075de4ef678c3d3139d4e')

KERNEL_RELEASE="$(uname -r)"

build() {
  local bArch
  
  cd "${srcdir}"
  patch -Np1 -i "sd_wlan.diff"
  cd SD_WLAN/SD-WLAN-BT-8688-LINUX2625-SAAR-10.49.3.p70-26609.P64_B1/wlan_src

  [ x$CARCH = xarm ] && bArch=arm
  [ x$CARCH = xi686 ] || [ x$CARCH = xx86_64 ] && bArch=x86

  make KERNELDIR="/usr/src/linux-$KERNEL_RELEASE" CROSS_COMPILE="" ARCH=$bArch
  
  gzip -c "sd8xxx.ko" > "sd8xxx.ko.gz"
  gzip -c "mcypt.ko" > "mcypt.ko.gz"
  
}

package() {
  cd "$srcdir/SD_WLAN/SD-WLAN-BT-8688-LINUX2625-SAAR-10.49.3.p70-26609.P64_B1/wlan_src"

  install -Dm644 sd8xxx.ko.gz "$pkgdir/lib/modules/$KERNEL_RELEASE/kernel/drivers/net/wireless/sd8688/sd8xxx.ko.gz"
  install -Dm644 mcypt.ko.gz "$pkgdir/lib/modules/$KERNEL_RELEASE/kernel/drivers/net/wireless/sd8688/mcypt.ko.gz"
}
