# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>
# Contributor: osily <ly50247@gmail.com>

_pkgname=flashplayer-standalone
pkgname=bin32-${_pkgname}
depends=('lib32-gtk2' 'lib32-libxt' 'lib32-nss' 'lib32-curl' 'desktop-file-utils' 'hicolor-icon-theme')
pkgver=11.2.202.238
pkgrel=1
pkgdesc="Macromedia standalone flash player (nonfree)"
url="http://www.adobe.com/support/flashplayer/downloads.html"
license="custom"
arch=('x86_64')
provides=("flashplayer-standalone")
conflicts=("flashplayer-standalone")
# License matrix: http://www.adobe.com/products/eulas/
source=("http://fpdownload.macromedia.com/pub/flashplayer/updaters/11/flashplayer_11_sa.i386.tar.gz"
        "http://www.adobe.com/products/eulas/pdfs/PlatformClients_PC_WWEULA-MULTI-20110809_1357.pdf"
        "flashplayer.desktop")
md5sums=('7c13f2cca4d618dde3cabb338218f9f2'
         '620a140c7e85af655f39f1b583fbf932'
         '84ffd464032c266913fdfb321aeb9066')
options=('!strip')
install="${_pkgname}.install"

package() {
  install -Dm644 "${srcdir}/flashplayer.desktop" "${pkgdir}/usr/share/applications/flashplayer.desktop"
  install -Dm644 "${srcdir}/PlatformClients_PC_WWEULA-MULTI-20110809_1357.pdf" "$pkgdir/usr/share/licenses/$pkgname/PlatformClients_PC_WWEULA-MULTI-20110809_1357.pdf"
  cd "${srcdir}/install_flash_player_linux_sa"
  install -Dm755 flashplayer "${pkgdir}/usr/bin/flashplayer"
  local d
  for d in usr/share/application-registry usr/share/mime-info usr/share/mimelnk usr/share/icons/hicolor; do
    install -dm755 "$pkgdir/$d"
    cp -R "$d/"* "$pkgdir/$d"
  done
}
