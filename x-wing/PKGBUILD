# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgname=x-wing
pkgver=1.5
pkgrel=1
pkgdesc="Space flight and combat simulator set in the Star Wars universe (uses DOSBox)"
arch=("any")
depends=("dosbox" "modfs-git")
makedepends=("convmv")
license=("custom")
url="http://www.lucasarts.com"
source=("http://www.softicons.com/download/tv-movie-icons/star-wars-icons-by-archigraphs/png/512/XWing_archigraphs.png"
        "lib${pkgname}.sh"
        "${pkgname}.sh"
        "${pkgname}-config.sh"
        "${pkgname}.desktop"
        "${pkgname}-config.desktop"
        "01-user.conf"
        "99-data.conf")
md5sums=('e90c27f0ec3ebfa4fafa725c4c793403'
         '772a228764b07c21c825852a453b3ea9'
         'f1a45028836ae200e8b8a97d0558e49b'
         'f99003033fdf72fc6cd2efea327f0531'
         '4f23cc4f9b78bd616fce15a995ad1ade'
         '52b0662ba73f60b9d6082d1b8ff75c38'
         '679f9097ff6055620425d607b1b758e9'
         '5bffc7973aede5aa5fa5791022ebcebb')

# Note: this PKGBUILD only works with the X-Wing Collector's CDROM edition of the game,
# *not* with the Floppy Disks edition nor the X-Wing Collector Series edition.

# Change this to the path where your X-Wing Collector's CDROM is mounted
: ${CD_DIR:="$HOME/media/Star_Wars__X-Wing_Collector's_CDROM_(US).iso/"}

package() {
  local dataDir="${pkgdir}/usr/share/games/${pkgname}/data"

  # Copy data (case insensitive)
  cd "${CD_DIR}"

  local f
  for f in "./*.mdr" "./*.wdr" "./*.ovl" "./bootdisk.exe" "./bwing.exe" "./detect.exe" "./imuse.*" "./install.exe" \
           "./*.fnt" "./*.cfg" "./*.plt" "./read.me" "./test*" "./setmuse.*" "./*.xid" "./*.pac" "./classic/*" \
           "./cp/*" "./mission/*" "./resource/*" "./talk/**/*"; do
    find . -iwholename "${f}" -exec install -Dm644 "{}" "${dataDir}/{}" \;
  done

  # Fix case
  cd "${pkgdir}/usr/share/games/${pkgname}"
  convmv --lower --notest -r *

  # Shell scripts
  install -Dm644 "${srcdir}/lib${pkgname}.sh" "${pkgdir}/usr/lib/lib${pkgname}.sh"
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 "${srcdir}/${pkgname}-config.sh" "${pkgdir}/usr/bin/${pkgname}-config"

  # .desktop file
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${srcdir}/${pkgname}-config.desktop" "${pkgdir}/usr/share/applications/${pkgname}-config.desktop"

  # Icon
  install -Dm644 "${srcdir}/XWing_archigraphs.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # Modfs
  install -Dm644 "${srcdir}/01-user.conf" "${pkgdir}/etc/${pkgname}.d/01-user.conf"
  install -Dm644 "${srcdir}/99-data.conf" "${pkgdir}/etc/${pkgname}.d/99-data.conf"
}
