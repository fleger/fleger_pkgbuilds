
# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgname=tiefighter
pkgver=1.0
pkgrel=3
pkgdesc="Space flight and combat simulator set in the Star Wars universe (uses DOSBox)"
arch=("any")
depends=("dosbox" "dos32a" "modfs-git")
makedepends=("unzip" "imagemagick" "convmv")
license=("custom")
url="http://www.lucasarts.com"
source=("ftp://ftp.lucasarts.com/patches/pc/tiecdjoy.exe"
        "lib${pkgname}.sh"
        "${pkgname}.sh"
        "${pkgname}-config.sh"
        "${pkgname}.desktop"
        "${pkgname}-config.desktop"
        "01-user.conf"
        "99-data.conf")
md5sums=('4153517873ca47afc92e12ed3005c826'
         'ae9cb090978684216f471aad7c63046f'
         '89dd5ae215637fb48ee7ffb992aadd2b'
         '9d0bf8a3e1d2712b57509db964f60c0c'
         '8d2530ec5036b4cf8f5ab5364e1ac7f7'
         'cc6577266586b609b295a9659301c41a'
         '6593501ab80ed4d26dc835e23a213298'
         '2d8bed59d3d03e5793eec9b6e69dff63')

# Note: this PKGBUILD only works with the Tie Fighter Collector's CDROM edition of the game,
# *not* with the Floppy Disks edition nor the X-Wing Collector Series edition (aka TIE95).

# Change this to the path where your Tie Fighter Collector's CDROM is mounted
: ${CD_DIR:="$HOME/media/Star_Wars__Tie_Fighter_Collector's_CDROM_(US).iso/"}

build() {
  cd "${srcdir}"
  convert "${CD_DIR}/tie01.ico" "${pkgname}.png"
  unzip -o tiecdjoy.exe
}

package() {
  local diskDir="${pkgdir}/usr/share/games/${pkgname}/disk"
  local dataDir="${pkgdir}/usr/share/games/${pkgname}/data"

  # Copy data (case insensitive)
  cd "${CD_DIR}"

  local f
  for f in "./detvesa.exe" "./*.fnt" "read.me" "./*.dat" "./tie.exe" "./uvconfig.exe" "./vga.pac" \
           "./z_tie2__.exe" "./astream/*" "./cp*/*.lfd" "./cp*/*.pnl" "./cp*/*.int" "./mission/*" \
           "./res*/*" "./voice/**/*" "./imuse.exe"; do
    find . -iwholename "${f}" -exec install -Dm644 "{}" "${diskDir}/{}" \;
  done

  for f in "imuse.ini" "*.tfr"; do
    find . -maxdepth 1 -iname "${f}" -exec install -Dm644 "{}" "${dataDir}/{}" \;
  done

  # imuse.exe must be present on both diskDir and dataDir
  ln -s "/usr/share/games/${pkgname}/disk/imuse.exe" "${dataDir}/imuse.exe"

  # Use DOS32/A instead of DOS4/GW
  ln -s "/usr/share/dos32a/dos32a.exe" "${diskDir}/dos4gw.exe"

  # Tell where Tie Fighter is installed
  echo -n 'C:\' > "${dataDir}/tie.cd"

  # Fix case
  cd "${pkgdir}/usr/share/games/${pkgname}"
  convmv --lower --notest -r *

  # Joystick patch
  install -Dm644 "${srcdir}/z_tie__.exe" "${diskDir}/z_tie__.exe"

  # Shell scripts
  install -Dm644 "${srcdir}/lib${pkgname}.sh" "${pkgdir}/usr/lib/lib${pkgname}.sh"
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 "${srcdir}/${pkgname}-config.sh" "${pkgdir}/usr/bin/${pkgname}-config"

  # .desktop file
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${srcdir}/${pkgname}-config.desktop" "${pkgdir}/usr/share/applications/${pkgname}-config.desktop"

  # Icon
  install -Dm644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # Modfs
  install -Dm644 "${srcdir}/01-user.conf" "${pkgdir}/etc/${pkgname}.d/01-user.conf"
  install -Dm644 "${srcdir}/99-data.conf" "${pkgdir}/etc/${pkgname}.d/99-data.conf"
}
