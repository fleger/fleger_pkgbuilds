#Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>
pkgname=modfs-git
pkgver=r4.4692b0c
pkgrel=1
pkgdesc="Wrapper around unionfs-fuse that uses a file-based branches configuration."
arch=(any)
url="https://gist.github.com/849049"
license=('custom:WTFPL')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
groups=()
depends=('unionfs-fuse')
makedepends=('git')
source=("${pkgname%-git}::git+https://github.com/gist/849049.git"
        'http://www.wtfpl.net/txt/copying')
md5sums=('SKIP'
         '8365d07beeb5f39d87e846dca3ae7b64')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  install -Dm755 "$srcdir/${pkgname%-git}/modfs.sh" "$pkgdir/usr/bin/modfs"
  install -Dm644 "$srcdir/copying" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
} 
